import com.liferay.gradle.util.OSDetector

buildscript {
   dependencies {
      classpath group: "com.liferay", name: "com.liferay.gradle.util", version: "latest.release"
   }
   repositories {
      mavenCentral()
   }
}

configure(subprojects.findAll { !it.subprojects }) {
    if (project.path.startsWith(":modules:") && project.file("bnd.bnd").exists()) {
        afterEvaluate { project ->
            dependencies {
                compileOnly group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
                compileOnly group: "com.liferay", name: "com.liferay.frontend.taglib"
                compileOnly group: "com.liferay", name: "com.liferay.frontend.taglib.clay"
                compileOnly group: "com.liferay", name: "com.liferay.osgi.util"
                compileOnly group: "com.liferay", name: "com.liferay.petra.function"
                compileOnly group: "com.liferay", name: "com.liferay.petra.lang"
                compileOnly group: "com.liferay", name: "com.liferay.petra.sql.dsl.api", version: "1.0.2"
                compileOnly group: "com.liferay", name: "com.liferay.petra.string"
                compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel"
                compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib"
                compileOnly group: "javax.portlet", name: "portlet-api"
                compileOnly group: "javax.servlet", name: "servlet-api", version: "2.5"
                compileOnly group: "jstl", name: "jstl"
                compileOnly group: "org.osgi", name: "org.osgi.core"
                compileOnly group: "org.osgi", name: "osgi.cmpn"

                testCompile group: "com.liferay", name: "com.liferay.petra.string"
                testCompile group: "com.liferay.portal", name: "com.liferay.portal.kernel"
                testCompile group: "javax.portlet", name: "portlet-api"
                testCompile group: "org.osgi", name: "org.osgi.core"
            }

            compileJava {
                options.encoding = 'UTF-8'
            }

            bundle {
                instruction "-fixupmessages", "annotations are deprecated"
            }

            if (project.path.contains(":test:") || project.path.contains("-test")) {
                deploy {
                    enabled = false
                }
            }
        }
    }
}

subprojects {
    plugins.withId("com.liferay.node") {
        def nodeArch
        def nodeExt
        def nodeOS
        def nodeVersion

        if (OSDetector.getBitmode().equals("32")) 
            nodeArch = "x86"
        else
            nodeArch = "x64"

        if (OSDetector.isWindows()) 
            nodeExt = "zip"
        else
            nodeExt = "tar.gz"

        if (OSDetector.isApple()) 
            nodeOS = "darwin";
        else if (OSDetector.isWindows()) 
            nodeOS = "win";
        else
            nodeOS = "linux"

        nodeVersion = "10.15.1";

        node {
            nodeUrl = "https://nodejs.org/dist/v${nodeVersion}/node-v${nodeVersion}-${nodeOS}-${nodeArch}.${nodeExt}"
        }
    }
}

distBundle() {
    doFirst {
        println "Adding all non-common configs into the common folder. This allows us to make a single bundle which contains configs for all environments."

        copy {
            from "${projectDir}/configs"
            exclude "common"
            exclude "local"
            into "${projectDir}/configs/common/configs"
        }

        //new File("${projectDir}/configs/common", ".gitignore").text = "# Autogenerated from addAllConfigs task\nconfigs"
    }

    doLast {
        println "Removing configs that were temporarily moved into the '/configs/common' folder."

        delete "${projectDir}/configs/common/configs"

        // Delete all non-Liferay files since these are not needed for CMIC's build artifact (also speeds up file transfer and less code for security scans)

        println "Removing non-Liferay files which are not needed in build artifact"

        delete "${projectDir}/build/dist/data/elasticsearch6"
        delete "${projectDir}/build/dist/logs"
        delete "${projectDir}/build/dist/osgi/state"
        delete "${projectDir}/build/dist/patching-tool"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/appclient"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/bin"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/docs"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/domain"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/jboss-modules.jar"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/modules/com/microsoft"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/modules/system"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/standalone/configuration"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/standalone/data"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/standalone/lib"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/standalone/log"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/standalone/tmp"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/welcome-content"
        delete "${projectDir}/build/dist/work"

        // Delete unneccessary files (patching files, unused libraries) to reduce risks reported in Black Duck

        println "Removing unused files and libraries"

        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/standalone/deployments/ROOT.war/WEB-INF/patching-backup.zip"
        delete "${projectDir}/build/dist/wildfly-16.0.0.Final/standalone/deployments/ROOT.war/WEB-INF/patching-backup-deps.zip"
    }
}

initBundle() {
    doLast {
        // Delete unneccessary files (patching files, unused libraries) to reduce risks reported in Black Duck

        println "Removing unused files and libraries"

        delete "${projectDir}/bundles/wildfly-16.0.0.Final/standalone/deployments/ROOT.war/WEB-INF/patching-backup.zip"
        delete "${projectDir}/bundles/wildfly-16.0.0.Final/standalone/deployments/ROOT.war/WEB-INF/patching-backup-deps.zip"
    }
}

